group 'net.mm2d'
version versionName

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'

archivesBaseName = 'mmupnp'

sourceCompatibility = 1.7
targetCompatibility = 1.7

test.maxParallelForks = 4

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

javadoc {
    title = "mmupnp $version API"
    description "Generates Javadoc."
    options.charSet = 'UTF-8'
    options.encoding = "UTF-8"
    options.links("http://docs.oracle.com/javase/jp/7/api/")
    options.stylesheetFile = new File("stylesheet.css")
}

dependencies {
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'net.mm2d:log:0.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.9.0'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'file:build/maven')
            pom.project {
                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
