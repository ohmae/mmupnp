apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco'

def versionMajor = 2
def versionMinor = 0
def versionPatch = 0
def versionSuffix = "-alpha8"
def packageName = 'net.mm2d'
def libraryId = 'mmupnp'
def siteUrl = 'https://github.com/ohmae/mmupnp'
def githubUrl = 'https://github.com/ohmae/mmupnp'

version "${versionMajor}.${versionMinor}.${versionPatch}${versionSuffix}"
group packageName
archivesBaseName = libraryId

sourceCompatibility = 1.8
targetCompatibility = 1.8

test.maxParallelForks = 4

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'net.mm2d:log:0.0.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.mockito:mockito-core:2.18.3'
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''
    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = project.group + '.' + libraryId
        licenses = ['MIT']
        websiteUrl = siteUrl
        issueTrackerUrl = githubUrl + '/blob/master/LICENSE'
        vcsUrl = githubUrl + '.git'
        issueTrackerUrl = githubUrl + '/issues'
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name libraryId
            url siteUrl

            packaging 'jar'
            groupId project.group
            artifactId libraryId
            version project.version

            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            scm {
                connection githubUrl + '.git'
                url githubUrl
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'file:build/maven')
            pom.project {
                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

javadoc {
    title = "mmupnp $version API"
    description "Generates Javadoc."
    options {
        charSet = 'UTF-8'
        encoding = "UTF-8"
        links("https://docs.oracle.com/javase/jp/7/api/")
        stylesheetFile = new File("stylesheet.css")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

jacoco {
    toolVersion = '0.8.1'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}
